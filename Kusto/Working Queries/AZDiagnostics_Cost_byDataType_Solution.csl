// Find total cost of GB for AzDiag
let AzDiagCost = todecimal(toscalar(Usage
| where TimeGenerated >ago(30d)
and DataType == "AzureDiagnostics"
| summarize GB = sum(Quantity / 1024) by Solution
| order by GB
| project Cost = GB * 2.3));
// Calculate total billable AzDiag rows, convert to decimal
let AZDcount = todecimal(toscalar(AzureDiagnostics
    | where TimeGenerated >= ago(30d)
    and _IsBillable == "True" 
    | count));
// Calculate percent of billable total by Category and Resource Id
AzureDiagnostics
| where TimeGenerated >= ago(30d)
| summarize RecordCount = count() by Category, _ResourceId
| order by RecordCount desc
| extend Percent_of_Total = todecimal(RecordCount / AZDcount)
| extend Resource = tostring(split(_ResourceId, "/")[-1])
| extend Cost = strcat("$", round((AzDiagCost * Percent_of_Total), 2)), Percent_of_Total = strcat(round((Percent_of_Total * 100),4), "%")
| project-reorder _ResourceId, Resource, Category, Percent_of_Total, Cost, RecordCount 
| project-away _ResourceId
| render piechart

